<?xml version="1.0"?>

<!--
                    GNU GENERAL PUBLIC LICENSE
                       Version 2, June 1991

 Copyright (C) 1989, 1991 Free Software Foundation, Inc.
                       59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 Everyone is permitted to copy and distribute verbatim copies
 of this license document, but changing it is not allowed.

                            Preamble

  The licenses for most software are designed to take away your
freedom to share and change it.  By contrast, the GNU General Public
License is intended to guarantee your freedom to share and change free
software-to make sure the software is free for all its users.  This
General Public License applies to most of the Free Software
Foundation's software and to any other program whose authors commit to
using it.  (Some other Free Software Foundation software is covered by
the GNU Library General Public License instead.)  You can apply it to
your programs, too.

  When we speak of free software, we are referring to freedom, not
price.  Our General Public Licenses are designed to make sure that you
have the freedom to distribute copies of free software (and charge for
this service if you wish), that you receive source code or can get it
if you want it, that you can change the software or use pieces of it
in new free programs; and that you know you can do these things.

  To protect your rights, we need to make restrictions that forbid
anyone to deny you these rights or to ask you to surrender the rights.
These restrictions translate to certain responsibilities for you if you
distribute copies of the software, or if you modify it.

  For example, if you distribute copies of such a program, whether
gratis or for a fee, you must give the recipients all the rights that
you have.  You must make sure that they, too, receive or can get the
source code.  And you must show them these terms so they know their
rights.

  We protect your rights with two steps: (1) copyright the software, and
(2) offer you this license which gives you legal permission to copy,
distribute and/or modify the software.

  Also, for each author's protection and ours, we want to make certain
that everyone understands that there is no warranty for this free
software.  If the software is modified by someone else and passed on, we
want its recipients to know that what they have is not the original, so
that any problems introduced by others will not reflect on the original
authors' reputations.

  Finally, any free program is threatened constantly by software
patents.  We wish to avoid the danger that redistributors of a free
program will individually obtain patent licenses, in effect making the
program proprietary.  To prevent this, we have made it clear that any
patent must be licensed for everyone's free use or not licensed at all.

  The precise terms and conditions for copying, distribution and
modification follow.

                    GNU GENERAL PUBLIC LICENSE
   TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION

  0. This License applies to any program or other work which contains
a notice placed by the copyright holder saying it may be distributed
under the terms of this General Public License.  The "Program", below,
refers to any such program or work, and a "work based on the Program"
means either the Program or any derivative work under copyright law:
that is to say, a work containing the Program or a portion of it,
either verbatim or with modifications and/or translated into another
language.  (Hereinafter, translation is included without limitation in
the term "modification".)  Each licensee is addressed as "you".

Activities other than copying, distribution and modification are not
covered by this License; they are outside its scope.  The act of
running the Program is not restricted, and the output from the Program
is covered only if its contents constitute a work based on the
Program (independent of having been made by running the Program).
Whether that is true depends on what the Program does.

  1. You may copy and distribute verbatim copies of the Program's
source code as you receive it, in any medium, provided that you
conspicuously and appropriately publish on each copy an appropriate
copyright notice and disclaimer of warranty; keep intact all the
notices that refer to this License and to the absence of any warranty;
and give any other recipients of the Program a copy of this License
along with the Program.

You may charge a fee for the physical act of transferring a copy, and
you may at your option offer warranty protection in exchange for a fee.

  2. You may modify your copy or copies of the Program or any portion
of it, thus forming a work based on the Program, and copy and
distribute such modifications or work under the terms of Section 1
above, provided that you also meet all of these conditions:

    a) You must cause the modified files to carry prominent notices
    stating that you changed the files and the date of any change.

    b) You must cause any work that you distribute or publish, that in
    whole or in part contains or is derived from the Program or any
    part thereof, to be licensed as a whole at no charge to all third
    parties under the terms of this License.

    c) If the modified program normally reads commands interactively
    when run, you must cause it, when started running for such
    interactive use in the most ordinary way, to print or display an
    announcement including an appropriate copyright notice and a
    notice that there is no warranty (or else, saying that you provide
    a warranty) and that users may redistribute the program under
    these conditions, and telling the user how to view a copy of this
    License.  (Exception: if the Program itself is interactive but
    does not normally print such an announcement, your work based on
    the Program is not required to print an announcement.)

These requirements apply to the modified work as a whole.  If
identifiable sections of that work are not derived from the Program,
and can be reasonably considered independent and separate works in
themselves, then this License, and its terms, do not apply to those
sections when you distribute them as separate works.  But when you
distribute the same sections as part of a whole which is a work based
on the Program, the distribution of the whole must be on the terms of
this License, whose permissions for other licensees extend to the
entire whole, and thus to each and every part regardless of who wrote it.

Thus, it is not the intent of this section to claim rights or contest
your rights to work written entirely by you; rather, the intent is to
exercise the right to control the distribution of derivative or
collective works based on the Program.

In addition, mere aggregation of another work not based on the Program
with the Program (or with a work based on the Program) on a volume of
a storage or distribution medium does not bring the other work under
the scope of this License.

  3. You may copy and distribute the Program (or a work based on it,
under Section 2) in object code or executable form under the terms of
Sections 1 and 2 above provided that you also do one of the following:

    a) Accompany it with the complete corresponding machine-readable
    source code, which must be distributed under the terms of Sections
    1 and 2 above on a medium customarily used for software interchange; or,

    b) Accompany it with a written offer, valid for at least three
    years, to give any third party, for a charge no more than your
    cost of physically performing source distribution, a complete
    machine-readable copy of the corresponding source code, to be
    distributed under the terms of Sections 1 and 2 above on a medium
    customarily used for software interchange; or,

    c) Accompany it with the information you received as to the offer
    to distribute corresponding source code.  (This alternative is
    allowed only for noncommercial distribution and only if you
    received the program in object code or executable form with such
    an offer, in accord with Subsection b above.)

The source code for a work means the preferred form of the work for
making modifications to it.  For an executable work, complete source
code means all the source code for all modules it contains, plus any
associated interface definition files, plus the scripts used to
control compilation and installation of the executable.  However, as a
special exception, the source code distributed need not include
anything that is normally distributed (in either source or binary
form) with the major components (compiler, kernel, and so on) of the
operating system on which the executable runs, unless that component
itself accompanies the executable.

If distribution of executable or object code is made by offering
access to copy from a designated place, then offering equivalent
access to copy the source code from the same place counts as
distribution of the source code, even though third parties are not
compelled to copy the source along with the object code.

  4. You may not copy, modify, sublicense, or distribute the Program
except as expressly provided under this License.  Any attempt
otherwise to copy, modify, sublicense or distribute the Program is
void, and will automatically terminate your rights under this License.
However, parties who have received copies, or rights, from you under
this License will not have their licenses terminated so long as such
parties remain in full compliance.

  5. You are not required to accept this License, since you have not
signed it.  However, nothing else grants you permission to modify or
distribute the Program or its derivative works.  These actions are
prohibited by law if you do not accept this License.  Therefore, by
modifying or distributing the Program (or any work based on the
Program), you indicate your acceptance of this License to do so, and
all its terms and conditions for copying, distributing or modifying
the Program or works based on it.

  6. Each time you redistribute the Program (or any work based on the
Program), the recipient automatically receives a license from the
original licensor to copy, distribute or modify the Program subject to
these terms and conditions.  You may not impose any further
restrictions on the recipients' exercise of the rights granted herein.
You are not responsible for enforcing compliance by third parties to
this License.

  7. If, as a consequence of a court judgment or allegation of patent
infringement or for any other reason (not limited to patent issues),
conditions are imposed on you (whether by court order, agreement or
otherwise) that contradict the conditions of this License, they do not
excuse you from the conditions of this License.  If you cannot
distribute so as to satisfy simultaneously your obligations under this
License and any other pertinent obligations, then as a consequence you
may not distribute the Program at all.  For example, if a patent
license would not permit royalty-free redistribution of the Program by
all those who receive copies directly or indirectly through you, then
the only way you could satisfy both it and this License would be to
refrain entirely from distribution of the Program.

If any portion of this section is held invalid or unenforceable under
any particular circumstance, the balance of the section is intended to
apply and the section as a whole is intended to apply in other
circumstances.

It is not the purpose of this section to induce you to infringe any
patents or other property right claims or to contest validity of any
such claims; this section has the sole purpose of protecting the
integrity of the free software distribution system, which is
implemented by public license practices.  Many people have made
generous contributions to the wide range of software distributed
through that system in reliance on consistent application of that
system; it is up to the author/donor to decide if he or she is willing
to distribute software through any other system and a licensee cannot
impose that choice.

This section is intended to make thoroughly clear what is believed to
be a consequence of the rest of this License.

  8. If the distribution and/or use of the Program is restricted in
certain countries either by patents or by copyrighted interfaces, the
original copyright holder who places the Program under this License
may add an explicit geographical distribution limitation excluding
those countries, so that distribution is permitted only in or among
countries not thus excluded.  In such case, this License incorporates
the limitation as if written in the body of this License.

  9. The Free Software Foundation may publish revised and/or new versions
of the General Public License from time to time.  Such new versions will
be similar in spirit to the present version, but may differ in detail to
address new problems or concerns.

Each version is given a distinguishing version number.  If the Program
specifies a version number of this License which applies to it and "any
later version", you have the option of following the terms and conditions
either of that version or of any later version published by the Free
Software Foundation.  If the Program does not specify a version number of
this License, you may choose any version ever published by the Free Software
Foundation.

  10. If you wish to incorporate parts of the Program into other free
programs whose distribution conditions are different, write to the author
to ask for permission.  For software which is copyrighted by the Free
Software Foundation, write to the Free Software Foundation; we sometimes
make exceptions for this.  Our decision will be guided by the two goals
of preserving the free status of all derivatives of our free software and
of promoting the sharing and reuse of software generally.

                            NO WARRANTY

  11. BECAUSE THE PROGRAM IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE PROGRAM, TO THE EXTENT PERMITTED BY APPLICABLE LAW.  EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE PROGRAM "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED
OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  THE ENTIRE RISK AS
TO THE QUALITY AND PERFORMANCE OF THE PROGRAM IS WITH YOU.  SHOULD THE
PROGRAM PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL NECESSARY SERVICING,
REPAIR OR CORRECTION.

  12. IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE PROGRAM AS PERMITTED ABOVE, BE LIABLE TO YOU FOR DAMAGES,
INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL OR CONSEQUENTIAL DAMAGES ARISING
OUT OF THE USE OR INABILITY TO USE THE PROGRAM (INCLUDING BUT NOT LIMITED
TO LOSS OF DATA OR DATA BEING RENDERED INACCURATE OR LOSSES SUSTAINED BY
YOU OR THIRD PARTIES OR A FAILURE OF THE PROGRAM TO OPERATE WITH ANY OTHER
PROGRAMS), EVEN IF SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE
POSSIBILITY OF SUCH DAMAGES.

-->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:eml="eml://ecoinformatics.org/eml-2.1.0" version="1.0">
  <xsl:output method="xml" indent="yes"></xsl:output>
  <!--<xsl:strip-space elements="*"></xsl:strip-space>-->
  <xsl:param name="package-id" select="'newID'"/>
	
  <xsl:template match="/* ">
    <!--handle top level element-->
    <xsl:element name="eml:eml">   
      <!--<xsl:copy-of select="@*"></xsl:copy-of>-->
      <!--<xsl:attribute name="xsi:schemaLocation">eml://ecoinformatics.org/eml-2.1.0 eml.xsd</xsl:attribute>-->

	  <!--<xsl:copy-of select="namespace::*[not(starts-with(.,'eml://ecoinformatics.org/')) or . != 'http://www.w3.org/2001/XMLSchema-instance']"/>-->
      <xsl:for-each select="@*">
	   <xsl:choose>
	    <xsl:when test="namespace-uri()='http://www.w3.org/2001/XMLSchema-instance'">
        <xsl:attribute name="xsi:{local-name()}" namespace="{namespace-uri()}">
          <xsl:variable name="value" select="."></xsl:variable>
          <xsl:choose>
            <!--change eml200 and stmml to eml210 and stmml-1.1 in attribute-->
            <xsl:when test="contains($value, &quot;eml://ecoinformatics.org/eml-2.0.0&quot;)">
              <xsl:variable name="first-replace">
                 <xsl:call-template name="replace-string">
                    <xsl:with-param name="text" select="$value"></xsl:with-param>
                    <xsl:with-param name="replace" select="'eml://ecoinformatics.org/eml-2.0.0'"></xsl:with-param>
                    <xsl:with-param name="with" select="'eml://ecoinformatics.org/eml-2.1.0'"></xsl:with-param>
                 </xsl:call-template>
			   </xsl:variable>
			   <xsl:variable name="second-replace">
                 <xsl:call-template name="replace-string">
                    <xsl:with-param name="text" select="$first-replace"></xsl:with-param>
                    <xsl:with-param name="replace" select="'http://www.xml-cml.org/schema/stmml'"></xsl:with-param>
                    <xsl:with-param name="with" select="'http://www.xml-cml.org/schema/stmml-1.1'"></xsl:with-param>
                 </xsl:call-template>
			   </xsl:variable>
				<xsl:value-of select="$second-replace"></xsl:value-of>
            </xsl:when>
			 <!--change eml201 and stmml to eml210 and stmml-1.1 in attribute-->
            <xsl:when test="contains($value, 'eml://ecoinformatics.org/eml-2.0.1') or contains($value, 'http://www.xml-cml.org/schema/stmml')">
			  <xsl:variable name="first-replace">
                 <xsl:call-template name="replace-string">
                    <xsl:with-param name="text" select="$value"></xsl:with-param>
                    <xsl:with-param name="replace" select="'eml://ecoinformatics.org/eml-2.0.1'"></xsl:with-param>
                    <xsl:with-param name="with" select="'eml://ecoinformatics.org/eml-2.1.0'"></xsl:with-param>
                 </xsl:call-template>
			   </xsl:variable>
			   <xsl:variable name="second-replace">
                 <xsl:call-template name="replace-string">
                    <xsl:with-param name="text" select="$first-replace"></xsl:with-param>
                    <xsl:with-param name="replace" select="'http://www.xml-cml.org/schema/stmml'"></xsl:with-param>
                    <xsl:with-param name="with" select="'http://www.xml-cml.org/schema/stmml-1.1'"></xsl:with-param>
                 </xsl:call-template>
			   </xsl:variable>
				<xsl:value-of select="$second-replace"></xsl:value-of>
            </xsl:when>
            <xsl:otherwise>
              <xsl:value-of select="."></xsl:value-of>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:attribute>
		</xsl:when>
		<xsl:otherwise>
            <xsl:choose>
               <xsl:when test="name()='packageId'">
				   <!-- handle package id: if there is no given packageid, it will use the old one. Otherwise, using the given id-->
			       <xsl:attribute name="{name()}" namespace="{namespace-uri()}">
					    <xsl:choose>
							<xsl:when test="$package-id='newID'">
								<xsl:value-of select="."/>
							</xsl:when>
							<xsl:otherwise>
								<xsl:value-of select="$package-id"/>
							</xsl:otherwise>
						</xsl:choose>
			       </xsl:attribute>
               </xsl:when>
               <xsl:otherwise>
				  <xsl:attribute name="{name()}" namespace="{namespace-uri()}">
                        <xsl:value-of select="."/>
                   </xsl:attribute> 
               </xsl:otherwise>
            </xsl:choose>
		</xsl:otherwise>
       </xsl:choose>
      </xsl:for-each>
      <!--<xsl:attribute name="xsi:schemaLocation">
		  <xsl:value-of select='translate(@xsi:schemaLocation, "eml-2.0.1", "eml-2.1.0")'/>
	  </xsl:attribute>-->
      <!-- move the access sub tree to top level-->
      <xsl:apply-templates mode="copy-top-access-tree" select="/*/dataset/access"></xsl:apply-templates>
      <xsl:apply-templates mode="copy-top-access-tree" select="/*/citation/access"></xsl:apply-templates>
      <xsl:apply-templates mode="copy-top-access-tree" select="/*/software/access"></xsl:apply-templates>
      <xsl:apply-templates mode="copy-top-access-tree" select="/*/protocol/access"></xsl:apply-templates>

      <xsl:for-each select="/*/*">
        <xsl:choose>
          <xsl:when test="name()='dataset'">
            <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
              <xsl:copy-of select="@*"></xsl:copy-of>
              <xsl:apply-templates mode="handle-elements-under-main-module" select="."
              ></xsl:apply-templates>
            </xsl:element>
          </xsl:when>

          <xsl:when test="name()='citation'">
            <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
              <xsl:copy-of select="@*"></xsl:copy-of>
              <xsl:apply-templates mode="handle-elements-under-main-module" select="."
              ></xsl:apply-templates>
            </xsl:element>
          </xsl:when>

          <xsl:when test="name()='software'">
            <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
              <xsl:copy-of select="@*"></xsl:copy-of>
              <xsl:apply-templates mode="handle-elements-under-main-module" select="."
              ></xsl:apply-templates>
            </xsl:element>
          </xsl:when>

          <xsl:when test="name()='protocol'">
            <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
              <xsl:copy-of select="@*"></xsl:copy-of>
              <xsl:apply-templates mode="handle-elements-under-main-module" select="."
              ></xsl:apply-templates>
            </xsl:element>
          </xsl:when>

          <xsl:when test="name()='additionalMetadata'">
            <xsl:apply-templates mode="handle-additionalMetadata" select="."></xsl:apply-templates>
          </xsl:when>

        </xsl:choose>
      </xsl:for-each>
    </xsl:element>
	 <xsl:message terminate="no">
		 <xsl:call-template name="output_message4_warn"/>
	  </xsl:message>
  </xsl:template>

  <!-- handle make changes under main module (dataset, citation, protocol and software) -->
  <xsl:template mode="handle-elements-under-main-module" match="*">
    <xsl:for-each select="./*">
      <xsl:choose>
        <xsl:when test="name()='access'">
          <xsl:apply-templates mode="do-nothing" select="."></xsl:apply-templates>
        </xsl:when>
        <xsl:otherwise>
          <xsl:apply-templates select="."></xsl:apply-templates>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:for-each>
  </xsl:template>

  <!-- main template which will copy nodes recursively-->
  <xsl:template match="*">
    <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates></xsl:apply-templates>
    </xsl:element>
  </xsl:template>

  <!-- fixing attributeList/attribute/measurementScale/datetime -> .../dateTime -->
  <xsl:template match="attributeList/attribute/measurementScale/datetime">
    <xsl:element name="dateTime" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates mode="copy-no-ns" select="./*"></xsl:apply-templates>
    </xsl:element>
  </xsl:template>

  <!-- change the name of element form method to methods -->
  <xsl:template match="dataTable/method | spatialRaster/method | spatialVector/method 
	  | view/method | storedProcedure/method | otherEntity/method">
    <xsl:element name="methods" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates mode="copy-no-ns-with-access-move" select="./*"></xsl:apply-templates>
    </xsl:element>
  </xsl:template>

  <!-- change the name of element form method to methods -->
  <xsl:template match="attribute/method">
    <xsl:element name="methods" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates mode="copy-no-ns-with-access-move" select="./*"></xsl:apply-templates>
    </xsl:element>
  </xsl:template>
	
	
	<!-- since under element "method", it can have distribution access movement, so this template will handle that-->
    <!-- copy node and children without namespace -->
  <xsl:template mode="copy-no-ns-with-access-move" match="*">
	 <xsl:choose>
		 <xsl:when test="name()='distribution' and name(parent::node())='implementation' and name(../..)='software'">			  
			 <xsl:call-template name="move-access"/>
		  </xsl:when>
		 <xsl:otherwise>
		      <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
                <xsl:copy-of select="@*"></xsl:copy-of>
                <xsl:apply-templates mode="copy-no-ns-with-access-move"></xsl:apply-templates>
              </xsl:element>
		 </xsl:otherwise>
	 </xsl:choose>   
  </xsl:template>

     <!--template to match physical/distribution and software/implementation/distribution which need to move around access module-->
	<xsl:template match="physical/distribution | software/implementation/distribution">
		<xsl:call-template name="move-access"/>
	</xsl:template>
	
  <!-- Move the access tree of data file level from additionalMetadata part to physical/distribution or software/implementation/distribution part.
           If we find the id of physical/distribution is in aditionalMetadata/describe and it 
             has sibling of access subtree, copy the subtree to physical/distribution -->
  <xsl:template name="move-access">
    <xsl:element name="distribution" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates mode="copy-no-ns" select="./*"></xsl:apply-templates>
      <!--distribution doesn't have any access node yet. Move the subtree from addtionalMetadata to distribution-->
      <!--find the id in addtionalMetacat/describes-->
      <xsl:variable name="id" select="@id"></xsl:variable>
      <!-- count how many additionalMetadata/access describing same distribution is -->
      <xsl:variable name="countAccessTree"
        select="count(/*/additionalMetadata[describes=$id]/access | /*/additionalMetadata[describes=$id]/metadata/access)"></xsl:variable>
      <xsl:choose>
        <xsl:when test="$countAccessTree=1">
          <!-- only has one access tree, we need copy it to distribution-->
          <xsl:apply-templates mode="copy-no-ns"
            select="/*/additionalMetadata[describes=$id]/access | /*/additionalMetadata[describes=$id]/metadata/access"
          ></xsl:apply-templates>
        </xsl:when>
        <xsl:when test="$countAccessTree &gt; 1">
          <!-- has more than one access tree, we need merge them-->
          <!--This means document have two or more addtionalMetadata
					          with access tree describing same distribution.
                       		<additionalMetadata>
                          		<describes>100</describe>
                          		<access>...</access>
                        	</additionalMetadata>
						 	<additionalMetadata>
                          		<describes>100</describe>
                          		<access>...</access>
                        	</additionalMetadata>-->
          <xsl:variable name="totalOrderNumber"
            select="count(/*/additionalMetadata[describes=$id]/access | /*/additionalMetadata[describes=$id]/metadata/access)"></xsl:variable>
          <xsl:variable name="totalAllowFirstNumber"
            select="count(/*/additionalMetadata[describes=$id]/access[@order='allowFirst'] | /*/additionalMetadata[describes=$id]/metadata/access[@order='allowFirst'])"></xsl:variable>
          <xsl:variable name="totalDenyFirstNumber"
            select="count(/*/additionalMetadata[describes=$id]/access[@order='denyFirst'] | /*/additionalMetadata[describes=$id]/metadata/access[@order='denyFirst'])"></xsl:variable>
          <xsl:choose>
            <xsl:when
              test="$totalOrderNumber=$totalAllowFirstNumber or $totalOrderNumber=$totalDenyFirstNumber">
              <!-- all access subtrees have same order, just merge it-->
              <xsl:element name="access">
                <xsl:copy-of
                  select="/*/additionalMetadata[describes=$id]/access/@* | /*/additionalMetadata[describes=$id]/metadata/access/@*"></xsl:copy-of>
                <xsl:for-each
                  select="/*/additionalMetadata[describes=$id]/access | /*/additionalMetadata[describes=$id]/metadata/access">
                  <xsl:apply-templates mode="copy-no-ns" select="./*"></xsl:apply-templates>
                </xsl:for-each>
              </xsl:element>
            </xsl:when>
            <xsl:otherwise>
              <xsl:message terminate="no">
                <xsl:call-template name="output_message1_warn">
                <xsl:with-param name="current_node" select="current()"></xsl:with-param>
              </xsl:call-template> </xsl:message>
            </xsl:otherwise>
          </xsl:choose>
        </xsl:when>
      </xsl:choose>
    </xsl:element>
  </xsl:template>


  <!-- copy access tree under dataset(or protocol, software and citation) to the top level -->
  <xsl:template mode="copy-top-access-tree" match="*">
    <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
  </xsl:template>

  <!-- do nothing for this element (removing it)-->
  <xsl:template mode="do-nothing" match="*"> </xsl:template>

  <!-- copy node and children without namespace -->
  <!--<xsl:template mode="copy-no-ns" match="*">
    <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
      <xsl:copy-of select="@*"></xsl:copy-of>
      <xsl:apply-templates mode="copy-no-ns"></xsl:apply-templates>
    </xsl:element>
  </xsl:template>-->
	
	
	<!-- copy node and children without showing default namespace. 
		But if the namespace is stmml, the namespace will be changed to stmml-1.1 -->
  <xsl:template mode="copy-no-ns" match="*">
   <xsl:choose>
	  <!--handle stmml element and attribute-->
	 <xsl:when test="namespace-uri()='http://www.xml-cml.org/schema/stmml'">
        <xsl:element name="stmml:{local-name()}" namespace="http://www.xml-cml.org/schema/stmml-1.1">
              <xsl:for-each select="@*">
	              <xsl:choose>
	                 <xsl:when test="namespace-uri()='http://www.w3.org/2001/XMLSchema-instance'">
                         <xsl:attribute name="xsi:{local-name()}" namespace="http://www.w3.org/2001/XMLSchema-instance">
                              <xsl:variable name="value" select="."></xsl:variable>
                                 <xsl:choose>
                                      <!--change stmml to stmml-1.1 in attribute-->
                                         <xsl:when test="contains($value, &quot;http://www.xml-cml.org/schema/stmml&quot;)">
                                              <xsl:call-template name="replace-string">
                                                 <xsl:with-param name="text" select="$value"></xsl:with-param>
                                                  <xsl:with-param name="replace" select="'http://www.xml-cml.org/schema/stmml'"></xsl:with-param>
                                                  <xsl:with-param name="with" select="'http://www.xml-cml.org/schema/stmml-1.1'"></xsl:with-param>
                                              </xsl:call-template>
                                          </xsl:when>
                                          <xsl:otherwise>
                                            <xsl:value-of select="."></xsl:value-of>
                                          </xsl:otherwise>
                              </xsl:choose>
                        </xsl:attribute>
		           </xsl:when>
		           <xsl:otherwise>
			           <xsl:copy-of select="."></xsl:copy-of>
		           </xsl:otherwise>
               </xsl:choose>
           </xsl:for-each>
           <xsl:apply-templates mode="copy-no-ns"></xsl:apply-templates>
        </xsl:element>
	 </xsl:when>
	  <!--handle non-stmml element-->
	 <xsl:otherwise>
		 <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
            <xsl:copy-of select="@*"></xsl:copy-of>
            <xsl:apply-templates mode="copy-no-ns"></xsl:apply-templates>
         </xsl:element>
     </xsl:otherwise>
   </xsl:choose>
  </xsl:template>
	
	<!-- if the element has no element child and value is empty string or whitespace and element name is not recordDelimiter, physicalLineDelimiter and fieldDelimiter,
      the tranformation will be terminated. -->
  <xsl:template match="*[not(*) and ((text() and not(normalize-space(text()) != '')) or .='') 
       and  name(.) != 'recordDelimiter' and name(.) != 'physicalLineDelimiter' and name(.) != 'fieldDelimiter' ]">
      <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
            <xsl:copy-of select="@*"></xsl:copy-of>
            <xsl:value-of select="."/>
      </xsl:element>
      <xsl:message terminate="no">
        <xsl:call-template name="output_message3_warn">
          <xsl:with-param name="current_node" select="."/>
        </xsl:call-template>
      </xsl:message>
  </xsl:template>
    
  <xsl:template mode="copy-no-ns-with-access-move" match="*[not(*) and ((text() and not(normalize-space(text()) != '')) or .='') 
       and name(.) != 'recordDelimiter' and name(.) != 'physicalLineDelimiter' and name(.) != 'fieldDelimiter' ]">
      <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
            <xsl:copy-of select="@*"></xsl:copy-of>
            <xsl:value-of select="."/>
      </xsl:element>
      <xsl:message terminate="no">
        <xsl:call-template name="output_message3_warn">
          <xsl:with-param name="current_node" select="."/>
        </xsl:call-template>
      </xsl:message>
  </xsl:template>
    
  <xsl:template mode="copy-no-ns" match="*[not(*) and ((text() and not(normalize-space(text()) != '')) or .='') 
       and name(.) != 'recordDelimiter' and name(.) != 'physicalLineDelimiter' and name(.) != 'fieldDelimiter' ]">
      <xsl:element name="{name(.)}" namespace="{namespace-uri(.)}">
            <xsl:copy-of select="@*"></xsl:copy-of>
            <xsl:value-of select="."/>
      </xsl:element>
      <xsl:message terminate="no">
        <xsl:call-template name="output_message3_warn">
          <xsl:with-param name="current_node" select="."/>
        </xsl:call-template>
      </xsl:message>
  </xsl:template>


  <!--Handle additionMetadata part. Here are 4 scenarios:
         1. <additionalMetadata>
					<describes>100</describes>
                     <foo>.....</foo>
             </additionalMetacata>
             The result will be (foo deosn't euqal access):
             <additionalMetadata>
					<describes>100</describes>
                     <metadata><foo>.....</foo></metadata>
             </additionalMetacata>
         2. <additionalMetadata>
					<describes>100</describes>
					<describes>200</describes>
                     <access>.....</access>
             </additionalMetacata>
            Both 100 and 200 are referenced ids of pysical/distribtuion or software/implementation/distribution.
            Since we moved the access part to distribution element. If multiple access rules are present 
            which reference the same distribution node, they are combined in th order they appear in 
            additionalMetadata sections. A conflict can occur here if  "order" attributes dont match. Fail
            with an erroro message.
            
            Since both are referenced id (and no conflict), we don't need to keep the info. 
           So the output is blank - remvoing the additionalMetadata tree.
         3. <additionalMetadata>
					<describes>300</describes>
					<describes>400</describes>
                     <access>.....</access>
             </additionalMetacata>
            300 is the referenced ids of pysical/distribtuion or software/implementation/distribution. 
            But 400 is not. So output will be:
            <additionalMetadata>
					<describes>400</describes>
                     <metadata><access>.....</access></metadata>
             </additionalMetacata>
            And we will give an warning message: message #2 referenced node is a not distribution id .
		4. <additionalMetadata>
                     <access>.....</access>
             </additionalMetacata>
            Since no describes, no access tree will move. So output will be:
            <additionalMetadata>
                     <metadata><access>.....</access></metadata>
             </additionalMetacata>
            And we will give an warning message:message #2 No distribution id in addtionalMetadata/describes 
            .
	-->
  <xsl:template mode="handle-additionalMetadata" match="*">
    <!-- this param for passing a suspicious node to an output message -->
    <xsl:param name="current_node"/>
    <!-- test if this additionalMetadata part has "access" element-->
    <xsl:variable name="accessCount" select="count(access | metadata/access)"></xsl:variable>
	
    <xsl:choose>
      <xsl:when test="$accessCount &lt; 1">
        <!-- no access tree here. Scenario 1 -->
        <additionalMetadata>
          <xsl:for-each select="./*">
            <xsl:choose>
              <xsl:when test="name()='describes'">
                <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
              </xsl:when>
              <!--if it already has metadata tag under additionalMetadata, just copy it-->
              <xsl:when test="name()='metadata'">
                <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
              </xsl:when>
              <xsl:otherwise>
                <metadata>
                  <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
                </metadata>
              </xsl:otherwise>
            </xsl:choose>
          </xsl:for-each>
        </additionalMetadata>
      </xsl:when>
      <xsl:otherwise>
        <!--additionalMetadata has EML access child-->
        <xsl:variable name="describesCount" select="count(describes)"></xsl:variable>
        <xsl:choose>
          <xsl:when test="$describesCount=0">
            <!-- scenario 4 - only has access but no describes, leave it there-->
            <additionalMetadata>
              <xsl:for-each select="./*">
                <xsl:choose>
                  <xsl:when test="name()='metadata'">
                    <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
                  </xsl:when>
                  <xsl:otherwise>
                    <metadata>
                      <xsl:apply-templates mode="copy-no-ns" select="."></xsl:apply-templates>
                    </metadata>
                  </xsl:otherwise>
                </xsl:choose>
              </xsl:for-each>
              <xsl:message terminate="no">
                <xsl:call-template name="output_message2_warn">
                  <xsl:with-param name="current_node" select="current()"></xsl:with-param>
                </xsl:call-template>
              </xsl:message>
            </additionalMetadata>
          </xsl:when>
          <xsl:otherwise>
            <!-- Scenario 2 and 3 -->
			 <xsl:variable name="describes" select="./describes"/>
            <xsl:call-template name="handle-describe-access-in-additional-metadata">
              <!--select node-set of describe which doesn't refer physical/distribtuion or software/implmentation/distribution and it doesn't have both "denyFirst" and "allowFirst"-->
              <xsl:with-param name="describes-list"
                select="./describes[(not(//physical/distribution/@id =.) and not(//software/implementation/distribution/@id = .)) or
				   ((//physical/distribution/@id =. or //software/implementation/distribution/@id = .) and (//additionalMetadata[describes = $describes and (access[@order='allowFirst'] or metadata/access[@order='allowFirst'])] and //additionalMetadata[describes = $describes and (access[@order='denyFirst'] or metadata/access[@order='denyFirst'])]))]"
              ></xsl:with-param>
            </xsl:call-template>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- parameter desribes will have the node-set of "describe" which doesn't reference any physical/distribtuion or
		software/implmentation/distribution. If size of node-set is zero, the template will do nothing, in other it will
		remove the additionalMetadata. If the size is not zero, we will keep the desribes and access-->
  <xsl:template name="handle-describe-access-in-additional-metadata">
    <xsl:param name="describes-list"></xsl:param>
    <xsl:choose>
      <xsl:when test="count($describes-list)=0">
        <!--Scenario 2: do nothing since all desribes are reference to physical/distribtuion or software/implmentation/distribution-->
      </xsl:when>
      <xsl:otherwise>
        <!--Scenario 3: some desribes doesn't refer to physical/distribtuion or software/implmentation/distribution. We need to keep them-->
        <additionalMetadata>
          <xsl:call-template name="recursive-describes">
            <xsl:with-param name="describes" select="$describes-list"></xsl:with-param>
          </xsl:call-template>
          <metadata>
            <xsl:apply-templates mode="copy-no-ns" select="./access | ./metadata/access"
            ></xsl:apply-templates>
          </metadata>
        </additionalMetadata>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- Use a recursive way to print out describes which doesn't refer physical/distribtuion or software/implmentation/distribution -->
  <xsl:template name="recursive-describes">
    <xsl:param name="describes"></xsl:param>
    <xsl:param name="index" select="1"></xsl:param>
    <xsl:choose>
      <!--finish-->
      <xsl:when test="$index > count($describes)">
        <!-- do nothing-->
      </xsl:when>
      <xsl:otherwise>
        <describes>
          <xsl:value-of select="$describes[$index]"></xsl:value-of>
        </describes>
        <xsl:call-template name="recursive-describes">
          <xsl:with-param name="describes" select="$describes"></xsl:with-param>
          <xsl:with-param name="index" select="$index + 1"></xsl:with-param>
        </xsl:call-template>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!--Template to replace string "replace" by string "with" in given string "text"-->
  <xsl:template name="replace-string">
    <xsl:param name="text"></xsl:param>
    <xsl:param name="replace"></xsl:param>
    <xsl:param name="with"></xsl:param>
    <xsl:choose>
      <xsl:when test="contains($text,$replace)">
        <xsl:value-of select="substring-before($text,$replace)"></xsl:value-of>
        <xsl:value-of select="$with"></xsl:value-of>
        <xsl:call-template name="replace-string">
          <xsl:with-param name="text" select="substring-after($text,$replace)"></xsl:with-param>
          <xsl:with-param name="replace" select="$replace"></xsl:with-param>
          <xsl:with-param name="with" select="$with"></xsl:with-param>
        </xsl:call-template>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="$text"></xsl:value-of>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!-- 
    
    these templates output mesages for the user if an additionalMetdata section cant be moved. -->
  <!-- 
    this message for scenario 1, order attribute conflict -->
  <xsl:template name="output_message1_warn">
    <xsl:param name="current_node"/>
    <xsl:text>
      Warning:
      The EML 2.0.1 document you wish to transform has multiple access subtrees in addtionalMetadata 
      blocks which describe the same entity.  However, their "order" attributes do not match, so these 
      access rules cannot be combinded under the entity's distribution node. Please fix the incoming 
      (EML 2.0) document so that the  "order" attributes on all access nodes for an entity are the same.
      Example content to help locate the problem:     </xsl:text>
    <xsl:value-of select="$current_node"/>  </xsl:template>    
  <!-- 
    
    this message for scenario 4  -->
  <xsl:template name="output_message2_warn">
    <xsl:param name="current_node"/>
    <xsl:text>
      Warning: 
      This document doesn't follow  EML2.0.1 specs for including access trees. It appears to contain an
      additonalMetadata section with an access element, but there is no describes element referencing 
      the id of a physical/distribution or a software/implementation/distribution element (there might be 
      a reference to some other node, however). The rest of the document was successfully converted to 
      EML2.1, although the offending access tree was left as is. You should examine your incoming 
      document and change it to comply with EML 2.0.1 specs, or move this access tree using some 
      other method. 
      Example content to help locate a potential problem:     </xsl:text>
    <xsl:value-of select="$current_node"/>
  </xsl:template>
	
  <!-- message for empty string element-->
  <xsl:template name="output_message3_warn">
    <xsl:param name="current_node"/>
    <xsl:text>
      Warning:
      The EML 2.0.1 document has an empty string (or whitespace) in an element which cannot be empty in EML 2.1.0 sepcification - </xsl:text>
    <xsl:apply-templates select="$current_node" mode="get-full-path"/>
</xsl:template>    
	
	
	<!-- message for reminding  user  that the eml 210 document may be invalid-->
  <xsl:template name="output_message4_warn">
 
    <xsl:text>
      Warning:
      The EML 2.0.1 document has been transformed into an EML 2.1.0 document. However, you should check if it is valid. 
	  See http://knb.ecoinformatics.org/software/eml/eml-2.1.0/eml-210info.html for more information.</xsl:text>
    
</xsl:template>    
  <!-- 
    
    end of user message templates -->
	
	<!-- get full path of given element-->
    <xsl:template match="node()" mode="get-full-path">
      <xsl:for-each select="ancestor-or-self::*">
        <xsl:text>/</xsl:text><xsl:value-of select="name()" />
      </xsl:for-each>
    </xsl:template>





</xsl:stylesheet>
